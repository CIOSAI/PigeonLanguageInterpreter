Pigeon {
    Program = (Line|Block)*
    
    Block = "{" (Line|Block)* "}"

    Line = Statement Semi

    Statement = Declaration
              | Conditional
              | FunctionCall
              | Value

	FunctionCall = #(iden "(") listOf<(discard|Value), space> ")"

    Lambda = "(" listOf<Input, space> ")" Typing? "=>" Value
           | "(" listOf<Input, space> ")" Typing? "=>" Block

    Input = iden Typing

    Declaration = Declarator iden Typing? Value

    Declarator = "let" | "mut" | "set"

    Conditional = WhenUnless (Bool|FunctionCall|iden) Value
                | WhenUnless (Bool|FunctionCall|iden) Block

    WhenUnless = "when" | "unless"

    Tuple = "(" listOf<Value, space> ")"

    Array = "[" listOf<Value, space> "]"

    Value = Literal
          | Lambda
          | FunctionCall
          | Tuple
          | Array
          | iden
    
    Type = ArrayType
         | LambdaType
         | TupleType
         | primitiveType
         | customType
         
    Typing = Colon Type
    
    LambdaType = "(" listOf<Type, space> ")" Typing

    TupleType = "Tuple<" listOf<Type, space> ">"

    ArrayType = Type "[]"

    primitiveType = "Int" | "Float" | "Bool" | "String" | "null"

    customType = &upper iden

    Literal = Number
            | Bool
            | str
            | Nothing
    
    discard = "_"
    
    iden = ~illegalIden idenHead+ (digit|idenHead)*
    
    idenHead = ~illegalIden (letter|idenValidSymbol|"\u{100}".."\u{10FFFF}")

    illegalIden = "//"|"/*"|"*/"

    idenValidSymbol =
    "+"|"-"|"~"|"?"|"<"|">"|"*"|"^"|"#"|"%"|"&"|"="|"_"|"/"

    Number = "-"? Float
           | "-"? Int

    Float = #(digit+ "." digit*) --startedWithDigit
          | #("." digit+) --startedWithDot
    
    Int = #(digit+)
    
    Bool = "TRUE" | "FALSE"

    str = "`" strContent* "`"

    strContent = ~("`"|"\\") any --nonEscaped 
                | "\\" escaped --escaped
    
    escaped = "`" | "\\" | "b" | "f" | "n" | "r" | "t" | "v"

    Nothing = "null"

    space += comment

    comment = multilineComment
            | singlelineComment
    
    multilineComment = "/*" (~"*/" any)* "*/"

    singlelineComment = "//" #((~"\n" any)*)

    Semi = ";"

    Colon = ":"
}